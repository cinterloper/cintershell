plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
    id 'maven-publish'
}
def AWS_MVN_BUCKET = System.getenv("AWS_MVN_BUCKET")
def AWS_DEFAULT_REGION = System.getenv("AWS_DEFAULT_REGION")
def AWS_ACCESS_KEY_ID = System.getenv("AWS_ACCESS_KEY_ID")
def AWS_SECRET_ACCESS_KEY = System.getenv("AWS_SECRET_ACCESS_KEY")
def LOCAL_REPO_DIR = System.getenv("LOCAL_REPO_DIR")


repositories {
    jcenter()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    if (AWS_MVN_BUCKET && !AWS_MVN_BUCKET.allWhitespace) {
        maven {
            url "s3://$AWS_MVN_BUCKET/maven2"
            credentials(AwsCredentials) {
                accessKey AWS_ACCESS_KEY_ID
                secretKey AWS_SECRET_ACCESS_KEY
            }
        }
    }

}

publishing {
    repositories {
        mavenLocal()
        if (LOCAL_REPO_DIR && !LOCAL_REPO_DIR.allWhitespace)
            maven {
                url "file://${LOCAL_REPO_DIR}"
            }
        if (AWS_MVN_BUCKET && !AWS_MVN_BUCKET.allWhitespace) {
            maven {
                url "s3://$AWS_MVN_BUCKET/maven2"
                credentials(AwsCredentials) {
                    accessKey AWS_ACCESS_KEY_ID
                    secretKey AWS_SECRET_ACCESS_KEY
                }
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId 'net.iowntheinter'
            artifactId 'cintershell'
            from components.java
        }
    }
}

def libversion = '3.3.3'
if(System.getenv("VERTX_VERSION") != null)
    libversion = System.getenv("VERTX_VERSION")

version = "${libversion}-17"
sourceCompatibility = '1.8'

dependencies {
    compile "io.vertx:vertx-core:$libversion"
    compile "io.vertx:vertx-lang-groovy:$libversion"
    compile "io.vertx:vertx-auth-common:$libversion"
    compile "io.vertx:vertx-auth-shiro:$libversion"
    compile "io.vertx:vertx-shell:$libversion"
    compile 'ch.qos.logback:logback-classic:1.1.7'
    compile 'ch.qos.logback:logback-core:1.1.7'
    compile 'org.slf4j:slf4j-api:1.7.21'
}

mainClassName = 'io.vertx.core.Launcher'

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Verticle': 'cintershellExample'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}
